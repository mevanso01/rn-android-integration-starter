apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    lintOptions {
        warningsAsErrors true
        disable "GradleDependency"
    }

    defaultConfig {
        applicationId "com.twilio.video.quickstart"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode 1
        versionName "1.0"

        buildConfigField("String",
                "TWILIO_ACCESS_TOKEN",
                "\"${getSecretProperty("TWILIO_ACCESS_TOKEN", "TWILIO_ACCESS_TOKEN")}\"")
        buildConfigField("String",
                "TWILIO_ACCESS_TOKEN_SERVER",
                "\"${getSecretProperty("TWILIO_ACCESS_TOKEN_SERVER", "http://localhost:3000")}\"")
        buildConfigField("boolean",
                "USE_TOKEN_SERVER",
                "${getSecretProperty("USE_TOKEN_SERVER", false)}")
    }

    compileOptions {
        sourceCompatibility versions.java
        targetCompatibility versions.java
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // Specify that we want to split up the APK based on ABI
    splits {
        abi {
            // Enable ABI split
            enable true

            // Clear list of ABIs
            reset()

            // Specify each architecture currently supported by the Video SDK
            include "armeabi-v7a", "arm64-v8a", "x86", "x86_64"

            // Specify that we do not want an additional universal SDK
            universalApk false
        }
    }
}

project.ext.react = [
    entryFile: "index.js",
    enableHermes: false,  // clean and rebuild if changing
]

def jscFlavor = 'org.webkit:android-jsc:+'
def enableHermes = project.ext.react.get("enableHermes", false);

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'

    implementation "com.twilio:video-android:${versions.videoAndroid}"
    implementation "com.koushikdutta.ion:ion:${versions.ion}"
    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "androidx.legacy:legacy-preference-v14:1.0.0"
    implementation "com.google.android.material:material:1.1.0"
    implementation 'com.squareup.retrofit2:retrofit:2.8.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.8.0'

    implementation "com.facebook.react:react-native:+" // From node_modules

    if (enableHermes) {
        // For RN 0.60.x
//        def hermesPath = "../rnapp/node_modules/hermesvm/android/"

        // --- OR ----

        // for RN 0.61+
        def hermesPath = "../rnapp/node_modules/hermes-engine/android/";

        debugImplementation files(hermesPath + "hermes-debug.aar")
        releaseImplementation files(hermesPath + "hermes-release.aar")
    } else {
        implementation jscFlavor
    }
}
